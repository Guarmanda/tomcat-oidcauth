/*
 * Gradle build script.
 */

plugins {
	id 'java'
	id 'maven-publish'
}

sourceCompatibility = '17'
targetCompatibility = '17'

group = 'org.bsworks.catalina.authenticator.oidc'
version = '2.6.0'

task jar(type: Jar, overwrite: true) {}
jar.enabled = false
javadoc.enabled = false

sourceSets {
	mainTomcat10 {
		java {
			srcDirs = ['src/common/java', 'src/tomcat10/java']
		}
	}
}

task compileJava(type: JavaCompile, overwrite: true) {
	dependsOn compileMainTomcat10Java
}

ext.sharedManifest = manifest {
	attributes(
		'Implementation-Title': project.name,
		'Implementation-Version': project.version,
		'Implementation-Vendor': 'Boyle Software, Inc.'
	)
}


task mainTomcat10Jar(type: Jar) {
	archiveClassifier = 'tomcat10'
	from sourceSets.mainTomcat10.output
	manifest = project.manifest {
		from sharedManifest
	}
}

repositories {
	mavenCentral()
}

dependencies {
	mainTomcat10Implementation(
		'org.apache.tomcat:tomcat-catalina:[10.0.0,10.1.15]',
		'org.apache.tomcat:tomcat-juli:[10.0.0,10.1.15]',
		'org.apache.tomcat:tomcat-util:[10.0.0,10.1.15]'
	)
}

artifacts {
	archives(
		mainTomcat10Jar
	)
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			pom {
				name = 'OpenID Connect Authenticator'
				description = 'Apache Tomcat authenticator implementation that extends the standard form-based authenticator and adds OpenID Connect functionality.'
				url = 'https://github.com/Guarmanda/tomcat-oidcauth'
				inceptionYear = '2015'
				licenses {
					license {
						name = 'The Apache License, Version 2.0'
						url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
					}
				}
				organization {
					name = 'Boyle Software, Inc.'
					url = 'https://www.boylesoftware.com/'
				}
				developers {
					developer {
						id = 'levahim'
						name = 'Lev Himmelfarb'
						email = 'lev@boylesoftware.com'
						timezone = 'America/New_York'
					}
				}
				scm {
					connection = 'scm:git:https://github.com/Guarmanda/tomcat-oidcauth.git'
					developerConnection = 'scm:git:git@github.com:Guarmanda/tomcat-oidcauth.git'
				}
				issueManagement {
					system = 'GitHub'
					url = 'https://github.com/Guarmanda/tomcat-oidcauth/issues'
				}
			}
			artifact mainTomcat10Jar
		}
	}
	repositories {
		maven {
			name = 'local'
			url = "file://${buildDir}/repo"
		}
	}
}
